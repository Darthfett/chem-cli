chem = require 'chem'

{vec2d, Engine, Batch, Sound, Sprite, button} = chem
ani = chem.resources.animations

canvas = document.getElementById("game")
engine = new Engine(canvas)
engine.showLoadProgressBar()
engine.start()
canvas.focus()
chem.resources.on 'ready', !->
  batch = new Batch()
  boom = new Sound('sfx/boom.ogg')
  ship = new Sprite(ani.ship, {batch, pos: vec2d(200, 200), rotation: Math.PI / 2})
  shipVel = vec2d()
  const rotationSpeed = Math.PI * 0.04
  const thrustAmt = 0.1
  fpsLabel = engine.createFpsLabel()
  engine.on \update, !(dt, dx) ->
    ship.pos.add(shipVel)

    # rotate the ship with left and right arrow keys
    if engine.buttonState(button.KeyLeft)
      ship.rotation -= rotationSpeed * dx
    if engine.buttonState(button.KeyRight)
      ship.rotation += rotationSpeed * dx

    # apply forward and backward thrust with up and down arrow keys
    thrust = vec2d(Math.cos(ship.rotation), Math.sin(ship.rotation))
    if engine.buttonState(button.KeyUp)
      shipVel.add(thrust.scaled(thrustAmt * dx))
    if engine.buttonState(button.KeyDown)
      shipVel.sub(thrust.scaled(thrustAmt * dx))

    # press space to blow yourself up
    if engine.buttonJustPressed(button.KeySpace)
      boom.play()
      ship.setAnimation(ani.boom)
      ship.setFrameIndex(0)
      ship.on \animationend, !-> ship.delete()
  engine.on \draw, !(context) ->
    # clear canvas to black
    context.fillStyle = '#000000'
    context.fillRect(0, 0, engine.size.x, engine.size.y)

    # draw all sprites in batch
    batch.draw(context)

    # draw a little fps counter in the corner
    fpsLabel.draw(context)
