#depend "chem/vec2d"
#depend "chem/event_emitter"
#depend "private/button_offset"

const {Vec2d, EventEmitter, Button} = window@Chem
const {MOUSE_OFFSET, KEY_OFFSET} = window@Chem@Private
const _exports = window@Chem
const _private = window@Chem@Private

_exports.Engine = class Engine extends EventEmitter
  const target_fps = 60
  const min_fps = 20
  const max_frame_skips = target_fps - min_fps
  const target_spf = 1 / target_fps
  const fps_smoothness = 0.9
  const fps_one_frame_weight = 1.0 - fps_smoothness

  const requestAnimationFrame = window.requestAnimationFrame or \
    window.webkitRequestAnimationFrame or \
    window.mozRequestAnimationFrame or \
    window.oRequestAnimationFrame or \
    window.msRequestAnimationFrame or \
    !(cb) -> window.setTimeout(cb, target_spf * 1000)

  (@canvas) ->
    super()
    # add tabindex property to canvas so that it can receive keyboard input
    @canvas.tabIndex = 0

    @context = @canvas.getContext("2d")
    @size = new Vec2d(@canvas.width, @canvas.height)
    @fps = target_fps

  setSize: !(@size) ->
    @canvas.width = @size.x
    @canvas.height = @size.y

  start: !->
    @attachListeners()
    @startMainLoop()

  stop: !->
    @stopMainLoop()
    @removeListeners()

  buttonState: (button) -> !!@button_states[button]
  buttonJustPressed: (button) -> !!@btn_just_pressed[button]
  mousePos: -> @mouse_pos

  draw: !(batch) ->
    const now = new Date()

    for sprites of batch.sprites
      for id, sprite in sprites
        const frame = sprite.animation.frames[sprite.getFrameIndex()]
        @context.save()
        @context.translate sprite.pos.x, sprite.pos.y
        @context.scale sprite.scale.x, sprite.scale.y
        @context.rotate sprite.rotation
        @context.drawImage _private.spritesheet, frame.pos.x, frame.pos.y, \
          frame.size.x, frame.size.y, \
          -sprite.animation.anchor.x, -sprite.animation.anchor.y, \
          frame.size.x, frame.size.y
        @context.restore()

  drawFps: !->
    return unless @fps?
    @context.fillText "#{Math.round(@fps)} fps", 0, @size.y

  # private
  startMainLoop: !->
    last_draw = previous_update = new Date()
    @main_loop_on = true
    mainLoop = !~>
      now = new Date()
      delta = (now - previous_update) / 1000
      previous_update := now

      skip_count = 0
      while delta > target_spf and skip_count < max_frame_skips
        @callUpdate target_spf, 1
        skip_count += 1
        delta -= target_spf

      const multiplier = delta / target_spf
      @callUpdate delta, multiplier
      @emit 'draw', @context
      now = new Date()
      const time_to_draw = (now - last_draw) / 1000
      const fps = 1 / time_to_draw
      @fps = @fps * fps_smoothness + fps * fps_one_frame_weight
      last_draw := now

      if @main_loop_on
        requestAnimationFrame(mainLoop, @canvas)
    requestAnimationFrame(mainLoop, @canvas)

  callUpdate: !(dt, dx) ->
    @emit 'update', dt, dx
    @btn_just_pressed = {}

  attachListeners: !->
    @button_states = {}
    @btn_just_pressed = {}

    # disable right click context menu
    @canvas.addEventListener 'contextmenu', !(event) ->
      event.preventDefault()

    # mouse input
    @mouse_pos = new Vec2d(0, 0)
    forwardMouseEvent = !(name, event) ~>
      @mouse_pos = new Vec2d(event.offsetX, event.offsetY)
      @emit name, @mouse_pos, MOUSE_OFFSET + event.which
    @canvas.addEventListener 'mousemove', !(event) ->
      forwardMouseEvent 'mousemove', event
    @canvas.addEventListener 'mousedown', !(event) ~>
      const button_id = MOUSE_OFFSET + event.which
      @button_states[button_id] = true
      @btn_just_pressed[button_id] = true

      forwardMouseEvent 'mousedown', event
    @canvas.addEventListener 'mouseup', !(event) ~>
      @button_states[MOUSE_OFFSET + event.which] = false

      forwardMouseEvent 'mouseup', event

    # keyboard input
    @canvas.addEventListener 'keydown', (event) ~>
      const button_id = KEY_OFFSET + event.which
      @button_states[button_id] = true
      @btn_just_pressed[button_id] = true

      @emit 'keydown', button_id
      event.preventDefault()
      return false
    @canvas.addEventListener 'keyup', (event) ~>
      button_id = KEY_OFFSET + event.which
      @button_states[button_id] = false

      @emit 'keyup', button_id
      event.preventDefault()
      return false

  removeListeners: !->
    # TODO

  stopMainLoop: !->
    @main_loop_on = false

